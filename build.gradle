plugins {
	id 'java'
	id 'war'
	id 'eclipse'
	id 'org.springframework.boot' version '2.2.6.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id "org.beryx.jlink" version "2.17.2"
	id 'org.javamodularity.moduleplugin' version '1.6.0'
	id 'com.google.cloud.tools.jib' version '1.8.0'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.mocah'
version = '0.0.2-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

mainClassName = 'com.mocah.mindmath.server.ServerApplication'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

configurations {
	springAutoConfig { transitive = false }
}

configurations {
    compile.exclude module: 'jsonassert'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.apache.jena:jena-core:3.14.0'
	implementation 'com.google.code.gson:gson:2.8.6'
	implementation 'org.apache.httpcomponents:httpcore:4.4.13'
	implementation 'org.apache.httpcomponents:httpclient:4.5.12'
	runtimeOnly 'org.apache.derby:derby'
	
	implementation 'it.unibo.alice.tuprolog:tuprolog:3.3.0'
	
	springAutoConfig 'org.springframework.boot:spring-boot-autoconfigure'
	
	//implementation('org.springframework.boot:spring-boot-starter-test') {
	//	exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	//}
	
	implementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	implementation 'org.junit.jupiter:junit-jupiter-api'
	implementation 'org.junit.platform:junit-platform-launcher'
}

test {
	useJUnitPlatform()
}

jar {
	enabled = true
}

war {
	enabled = true
}

jlink {
	options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
	launcher {
		name = 'LaunchMindMath'
		customImage {
			appModules = ['com.mocah.merged.module']
		}
		jvmArgs = [
			'--add-reads', 'com.mocah.merged.module=com.mocah.mindmath',
			'-cp', '../app/*'
		]
	}
	mergedModule {
		additive = true
		uses 'ch.qos.logback.classic.spi.Configurator'
		uses 'javax.validation.valueextraction.ValueExtractor'
		uses 'javax.validation.ConstraintValidator'
		uses 'org.hibernate.integrator.spi.Integrator'
		uses 'org.hibernate.service.spi.ServiceContributor'
		
		uses 'org.hibernate.boot.registry.selector.StrategyRegistrationProvider'
		uses 'org.hibernate.boot.spi.MetadataSourcesContributor'
		uses 'org.hibernate.boot.spi.MetadataBuilderInitializer'
		uses 'org.hibernate.boot.spi.MetadataBuilderFactory'
		uses 'org.hibernate.boot.model.TypeContributor'
		uses 'org.hibernate.boot.spi.MetadataContributor'
		uses 'org.hibernate.boot.spi.AdditionalJaxbMappingProducer'
		uses 'org.hibernate.boot.spi.SessionFactoryBuilderFactory'
		uses 'org.hibernate.service.spi.SessionFactoryServiceContributor'
		
		excludeProvides implementation: 'com.sun.xml.bind.v2.ContextFactory'
		excludeProvides servicePattern: 'javax.enterprise.*'
	}
	
	forceMerge 'jackson', 'log4j', 'byte-buddy', 'hibernate'
}

prepareMergedJarsDir.doLast {
	// extract META-INF/spring.factories from spring-boot-autoconfigure
	copy {
		from zipTree(configurations.springAutoConfig.singleFile).matching {
			include 'META-INF/spring.factories'
		}
		into jlinkBasePath
	}

	// insert META-INF/spring.factories into the main jar
	ant.zip(update: "true", destfile: jar.archivePath, keepcompression: true) {
		fileset(dir: "$jlinkBasePath", includes: 'META-INF/**')
	}
}

eclipse {
	classpath {
		containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
		file {
			whenMerged {
				entries.findAll { it.properties.kind.equals('lib') || it.properties.path.contains("junit") }.each {
					it.entryAttributes['module'] = 'true'
				}
				entries.findAll { it.properties.path.startsWith('org.eclipse.jdt.launching.JRE_CONTAINER') }.each {
					it.entryAttributes['module'] = 'true'
				}
			}
		}
	}
}
